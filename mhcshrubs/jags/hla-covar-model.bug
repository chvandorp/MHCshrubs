## JAGS model, using a covariance matrix directly

data {
    ## transformed data (NB: in JAGS, nodes can only be defined once in the model block)
    for ( i in 1:N ) {
        oneA1[i] <- 1
        oneA2[i] <- 1
        oneB1[i] <- 1
        oneB2[i] <- 1
        oneC1[i] <- 1
        oneC2[i] <- 1
    }
    ## uninformed Dirichlet distribution...
    for ( h in 1:HA ) { halvesA[h] <- 0.5 }
    for ( h in 1:HB ) { halvesB[h] <- 0.5 }
    for ( h in 1:HC ) { halvesC[h] <- 0.5 }

	  ## for the multivariate-normal distribution, we need a 0 vector for the mean
	  for ( r in 1:R ) {
		    zeros[r] <- 0
    }

    ## we give a covariance matrix as input, but the dmnorm distribution needs the inverse
    Omega <- inverse(Sigma)

    ## precision for priors
    precision <- 1.0E-3
}

model {
    ## loop over the N patients
    for ( i in 1:N ) {
        log_like[i] <- log(dnorm(V[i], W[i], tau_V)) ## used for WAIC
        ## likelihood of virus loads V, given a "predicted virus load W"
        V[i] ~ dnorm(W[i], tau_V)
        intervalCensorValue[i] ~ dinterval(V[i], VLbound[i])
        ## the predicted virus load is determined by the weights (beta) of the HLA alleles
        ## each patient has 3 times 2 alleles
        hlaEffect[i] <- betaA[hlaA1[i]] + betaA[hlaA2[i]] +
                        betaB[hlaB1[i]] + betaB[hlaB2[i]] +
                        betaC[hlaC1[i]] + betaC[hlaC2[i]]
        W[i] <- alpha + hlaEffect[i] - meanHlaEffect

        ## missing HLA data. The vectors cA1[,i], ..., cC2[,i] restrict the possible
        ## alleles for patient i. If the 4-digit allele is fully known, only one
        ## element of the vector is 1, otherwise, the vector contains multiple 1s
        hlaA1[i] ~ dcat(cA1[,i])
        hlaA2[i] ~ dcat(cA2[,i])
        hlaB1[i] ~ dcat(cB1[,i])
        hlaB2[i] ~ dcat(cB2[,i])
        hlaC1[i] ~ dcat(cC1[,i])
        hlaC2[i] ~ dcat(cC2[,i])

        ## likelihood of HLA distribution. Each patient contributes to the likelihood
        ## of the HLA distributions pA, pB and pC.
        oneA1[i] ~ dbern(pA[hlaA1[i]]) ## oneA1 is mock "data" and always 1
        oneA2[i] ~ dbern(pA[hlaA2[i]])
        oneB1[i] ~ dbern(pB[hlaB1[i]])
        oneB2[i] ~ dbern(pB[hlaB2[i]])
        oneC1[i] ~ dbern(pC[hlaC1[i]])
        oneC2[i] ~ dbern(pC[hlaC2[i]])
    }
    ## subtract "meanHlaEffect" from W[i]'s to de-correlate alpha with betas
    ## and get better precision for both.
    meanHlaEffect <- mean(hlaEffect)

    ## precision of V given the prediction W
    tau_V ~ dgamma(precision, precision)

    ## HLA frequency distribution
    pA ~ ddirch(halvesA)
    pB ~ ddirch(halvesB)
    pC ~ ddirch(halvesC)

    ## additional data to inform the frequency distributions.
    ## We use allele counts mA, mB and mC from Sub-Saharan Africa (SSA)
    ## to better estimate the Allele frequencies. NB: some alleles
    ## in the Durban cohort, have 0 frequency in the SSA sample. Therefore,
    ## we cannot use the SSA frequencies directly.
    mA ~ dmulti(pA, MA)
    mB ~ dmulti(pB, MB)
    mC ~ dmulti(pC, MC)

    ## prior of the HLA effects. The intercept of the virus load model
    alpha ~ dnorm(0.0, precision)

    ## joint prior for the weights has scaled covariance matrix
    tau_beta ~ dgamma(precision, precision)

    ## multi-normal distribution for beta, Omega = Sigma^{-1}
    beta ~ dmnorm(zeros, tau_beta*Omega);

    ## make vectors with weights betaA, betaB, betaC
    for ( h in 1:HA ) {
        betaA[h] <- beta[IdxsSigmaA[h]]
    }
    for ( h in 1:HB ) {
        betaB[h] <- beta[IdxsSigmaB[h]]
    }
    for ( h in 1:HC ) {
        betaC[h] <- beta[IdxsSigmaC[h]]
    }


    ## compute R^2 of the model. See the paper
    ## Gelman et al., R-squared for Bayesian regression models, 2017
    sdW <- sd(W) ## standard deviation of the data
    sdE <- sd(V-W) ## standard deviation of the error
    R2 <- sdW * sdW / (sdW * sdW + sdE + sdE) ## "alternative" R^2
}
